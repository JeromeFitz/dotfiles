# ğŸ°ï¸ shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles
export BREW_PREFIX=/usr/local
# âš›ï¸ `c [tab]` project folder (move to private?)
export PROJECTS=~/Code

DEFAULT_USER=`whoami`

# ğŸ¥ªï¸ load sandboxd first
source $ZSH/zsh/plugins/sandboxd.plugin.zsh

# âš—ï¸ debugging
# zmodload zsh/zprof

# ğŸ”’ï¸ private `./dotfiles-private` repo stuff
if [[ -a ~/.zshrc.private ]]
then
  source ~/.zshrc.private
fi

# ğŸ’½ï¸ get all zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# ğŸ—ºï¸ load: path
for file in ${(M)config_files:#*/*.path.zsh}
do
  source $file
done

# ğŸ›ï¸ load: everything (except: path, completion, & sandboxd)
for file in ${${${config_files:#*/*.path.zsh}:#*/*.completion.zsh}:#*/sandboxd.plugin.zsh}
do
  source $file
done

# ğŸ§­ï¸ load: completion (after autocomplete loads)
autoload -Uz compinit && compinit
for file in ${(M)config_files:#*/*.completion.zsh}
do
  source $file
done

unset config_files

# ğŸ“œï¸ Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

# âš—ï¸ debugging
# zprof
