# üç∞Ô∏è shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles
export BREW_PREFIX=/usr/local
# ‚öõÔ∏è `c [tab]` project folder (move to private?)
export PROJECTS=~/Code

DEFAULT_USER=`whoami`

# ü•™Ô∏è load sandboxd first
source $ZSH/zsh/plugins/sandboxd.plugin.zsh

# ‚öóÔ∏è debugging
# zmodload zsh/zprof

# üîíÔ∏è private `./dotfiles-private` repo stuff
if [[ -a ~/.zshrc.private ]]
then
  source ~/.zshrc.private
fi

# # üíΩÔ∏è get all zsh files
# #
# # üî•Ô∏è Hard-code the `config_files` since these rarely change
# #    1) Uncomment => Run the `config_files` search
# #    2) Manually populate the hard-coded `config_files` variable
# # üí™Ô∏è Gains:
# #    BEFORE: 0.55 sys
# #    AFTER:  0.23 sys
# #

# typeset -U config_files
# config_files=($ZSH/**/*.zsh)
# echo $config_files

config_files=(
  # paths
  $ZSH/config/go.path.zsh
  $ZSH/config/homebrew.path.zsh
  $ZSH/config/java.path.zsh
  $ZSH/config/nvm.path.zsh
  $ZSH/config/python.path.zsh
  $ZSH/config/rust.path.zsh
  $ZSH/config/system.path.zsh
  $ZSH/config/yarn.path.zsh
  # !paths !completion
  $ZSH/config/database.alias.zsh
  $ZSH/config/docker.alias.zsh
  $ZSH/config/git.alias.zsh
  $ZSH/config/node.alias.zsh
  $ZSH/config/npm.alias.zsh
  $ZSH/config/ruby.alias.zsh
  $ZSH/config/system.alias.zsh
  $ZSH/config/system.env.zsh
  $ZSH/config/system.grc.zsh
  $ZSH/config/system.keys.zsh
  $ZSH/config/xcode.alias.zsh
  $ZSH/config/yarn.alias.zsh
  $ZSH/config/zsh.alias.zsh
  $ZSH/symlinks/rbenv.zsh
  $ZSH/zsh/config.zsh
  $ZSH/zsh/fpath.zsh
  $ZSH/zsh/plugins/history-substring-search.plugin.zsh
  $ZSH/zsh/prompt.zsh
  $ZSH/zsh/window.zsh
  # completion
  $ZSH/config/git.completion.zsh
  $ZSH/config/nvm.completion.zsh
  $ZSH/config/ruby.completion.zsh
  $ZSH/config/zsh.completion.zsh
)

# üó∫Ô∏è load: path
for file in ${(M)config_files:#*/*.path.zsh}
do
  source $file
done

# üõÅÔ∏è load: everything (except: path, completion, & sandboxd)
for file in ${${${config_files:#*/*.path.zsh}:#*/*.completion.zsh}:#*/sandboxd.plugin.zsh}
do
  source $file
done

# üß≠Ô∏è load: completion (after autocomplete loads)
autoload -Uz compinit && compinit
for file in ${(M)config_files:#*/*.completion.zsh}
do
  source $file
done

unset config_files

# üìúÔ∏è Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

# ‚öóÔ∏è debugging
# zprof
