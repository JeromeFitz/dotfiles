# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

DEFAULT_USER=`whoami`

# Stash your environment variables in ~/.zshrc.private. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.zshrc.private ]]
then
  source ~/.zshrc.private
fi

autoload colors && colors
autoload -Uz compinit && compinit

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
# echo "> !* path"
for file in ${(M)config_files:#*/*.path.zsh}
do
  source $file
done

# load everything but the path and completion files
# echo "> * !*.path|*.completion|nvm.*"
for file in ${${${config_files:#*/*.path.zsh}:#*/*.completion.zsh}:#*/nvm.*.zsh}
do
  # echo $file
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
# echo "> completion !*"
for file in ${(M)config_files:#*/*.completion.zsh}
do
  source $file
done

# @hack(nvm) load nvm last
# @todo(nvm) lazy load proper
# ref: https://stackoverflow.com/a/47883587
# load the nvm files
# echo "> nvm !*"
for file in ${(M)config_files:#*/nvm.*.zsh}
do
  source $file
done

unset config_files

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down
